
node('jenkins-agent-docker-sg2042') {

    def kernel_build_params
    def lava_trigger_params
    def script_content = ""

    stage("checkout scm") {
        cleanWs()
        node("jenkins-agent-lavacli-docker") {
            cleanWs()
            checkout scm
            load('config.groovy')

            kernel_build_params = kernel_build_params_keys.collect {
                all_params[it]
            }
            lava_trigger_params = lava_trigger_params_keys.findAll {
                                        params.containsKey(it)
                                    }.collect {
                                        string(name: it, value: params[it])
                                    }
            script_content = readFile 'rvck-kernel-build/run.sh'
        }
    }

    def isAnyParams = params.size() != 0
    

    stage("properties") {
        properties([
            parameters(kernel_build_params),
        ])
    }
    
    if(isAnyParams) {
       
        stage("build") {
            timestamps {
                try {
                    // 执行构建脚本
                    sh script_content
                    
                    // 更新github状态
                    build wait: false, job: 'gh_actions', parameters: [
                        string(name: 'REPO', value: REPO),
                        string(name: 'ISSUE_ID', value: ISSUE_ID),
                        string(name: 'COMMENT_CONTENT', value: 'Kernel build success!'),
                        string(name: 'SET_LABEL', value: 'kernel_build_succeed,lava_checking'),
                    ]
                    
                    // 触发lava测试
                    
                    
                    lava_trigger_params += [
                        string(name: 'rootfs_download_url', value: readFile('rootfs_download_url')),
                        string(name: 'kernel_download_url', value: readFile('kernel_download_url')),
                    ]
                    echo "lava_trigger_params: ${lava_trigger_params}"
                    
                    build wait: false, job: 'rvck-lava-trigger', parameters: lava_trigger_params
                } catch (err) {
                    
                    build wait: false, job: 'gh_actions', parameters: [
                        string(name: 'REPO', value: REPO),
                        string(name: 'ISSUE_ID', value: ISSUE_ID),
                        string(name: 'COMMENT_CONTENT', value: "Kernel build failed! log:${BUILD_URL}consoleFull"),
                        string(name: 'SET_LABEL', value: 'kernel_build_failed'),
                    ]

                    currentBuild.result = 'FAILURE'
                    throw err
                }
            }
        }
    }
    
}
